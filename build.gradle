buildscript {
    repositories {
        maven { url 'https://dl.bintray.com/alphagov/maven-test' }
        jcenter()
    }
    dependencies {
        classpath 'com.github.spullara.mustache.java:compiler:0.8.10',
                'org.yaml:snakeyaml:1.10',
                'com.google.guava:guava:14.0.1',
                'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

ext {
    opensaml_version = '3.4.0'
    dropwizard_version = '1.2.0'
    test_utils_version = '2.0.0-346'
    utils_version = '2.0.0-336'
    saml_libs_version = "${opensaml_version}-163"
    soft_hsm_version = '1.1.1'
    build_version = "$opensaml_version-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
}


subprojects {

    apply plugin: 'java'

    group = "uk.gov.ida"
    version = "0.1.$version"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        maven { url 'https://dl.bintray.com/alphagov/maven-test' }
        maven { url 'https://dl.bintray.com/alphagov/maven' }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
        jcenter()
    }

    sourceSets {
        test {
            resources.srcDirs += ["src/dist"]
        }
    }

    configurations {
        common
        dropwizard
        saml
        saml_test
        soft_hsm
        ida_utils
    }

    dependencies {
        dropwizard (
                "io.dropwizard:dropwizard-core:${dropwizard_version}",
                "io.dropwizard:dropwizard-client:${dropwizard_version}",
                "io.dropwizard:dropwizard-views-mustache:${dropwizard_version}"
        )

        saml (
                "org.opensaml:opensaml-core:${opensaml_version}",
                "org.opensaml:opensaml-saml-impl:${opensaml_version}",
                "org.opensaml:opensaml-storage-impl:${opensaml_version}",
                "uk.gov.ida:saml-extensions:${saml_libs_version}",
                "uk.gov.ida:saml-metadata-bindings:${saml_libs_version}",
                "uk.gov.ida:saml-security:${saml_libs_version}",
                "uk.gov.ida:saml-utils:${saml_libs_version}",
                "se.litsec.eidas:eidas-opensaml3:1.3.1",
                "se.litsec.opensaml:opensaml3-ext:1.2.2"
        )

        saml_test (
                "net.sourceforge.htmlunit:htmlunit:2.28",
                "uk.gov.ida:common-test-utils:${test_utils_version}",
                "uk.gov.ida:saml-metadata-bindings-test:${saml_libs_version}",
                "io.dropwizard:dropwizard-testing:${dropwizard_version}",
        )

        common (
                "javax.xml.bind:jaxb-api:2.3.0",
                "javax.activation:activation:1.1.1",
                "io.lettuce:lettuce-core:5.1.3.RELEASE",
                "uk.gov.ida:security-utils:${utils_version}"
        )

        soft_hsm (
                "se.swedenconnect.opensaml:opensaml-pkcs11-support:${soft_hsm_version}"
        )

        ida_utils (
                "uk.gov.ida:rest-utils:${utils_version}"
        )

        testCompile (
            "org.junit.jupiter:junit-jupiter-api:5.0.1",
            "org.junit.platform:junit-platform-launcher:1.0.1",
            "org.hamcrest:hamcrest-core:1.3",
            "org.hamcrest:hamcrest-library:1.3",
            "org.mockito:mockito-core:2.23.0",
            "io.dropwizard:dropwizard-testing:${dropwizard_version}",
            "net.sourceforge.htmlunit:htmlunit:2.28",
            project(':proxy-node-test')
        )

    }
}

defaultTasks 'clean', 'test'
